apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion app.compile_sdk_version

    defaultConfig {
        applicationId "ru.cleverpumpkin.calendar.sample"
        minSdkVersion app.min_sdk_version
        targetSdkVersion app.target_sdk_version
        versionCode app.version_code
        versionName app.version_name

    }

    signingConfigs {
        upload {
            keyAlias keystoreProperties['uploadKeyAlias']
            keyPassword keystoreProperties['uploadKeyPassword']
            storeFile file(keystoreProperties['uploadStoreFile'])
            storePassword keystoreProperties['uploadStorePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.upload
        }

        debug {
            applicationIdSuffix '.debug'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
    }

}

dependencies {
    implementation project(':crunchycalendar')

    // Kotlin language standard lib
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$libs.kotlin_version"

    // AndroidX: UI
    implementation "androidx.appcompat:appcompat:$libs.appCompatX"
    implementation "androidx.recyclerview:recyclerview:$libs.recyclerViewX"

    // ViewBindingPropertyDelegate
    implementation "com.github.kirich1409:viewbindingpropertydelegate-noreflection:$libs.viewBindingPropertyDelegate"
}
